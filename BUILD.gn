import("//build/ohos.gni")
import("//third_party/wayland_standard/wayland_protocol.gni")

## Generate Wayland Protocols {{{
wayland_protocol("text_cursor_position_protocol") {
  sources = [ "protocol/text-cursor-position.xml" ]
}
wayland_protocol("weston_content_protection_protocol") {
  sources = [ "protocol/weston-content-protection.xml" ]
}
wayland_protocol("weston_touch_calibration_protocol") {
  sources = [ "protocol/weston-touch-calibration.xml" ]
}
wayland_protocol("weston_direct_display_protocol") {
  sources = [ "protocol/weston-direct-display.xml" ]
}
wayland_protocol("weston_screenshooter_protocol") {
  sources = [ "protocol/weston-screenshooter.xml" ]
}
wayland_protocol("ivi_application_protocol") {
  sources = [ "protocol/ivi-application.xml" ]
}
wayland_protocol("drm_auth_protocol") {
  sources = [ "protocol/drm-auth.xml" ]
}

## Generate Wayland Protocols }}}

## Build libshared.a {{{
config("libshared_config") {
  visibility = [ ":*" ]

  include_dirs = [
    "include",
    "//third_party/wayland_standard/src",
    "//third_party/wayland-ivi-extension_standard/include",
  ]

  cflags = [
    "-Wall",
    "-Werror",
    "-Wno-unused-parameter",
  ]
}

config("libshared_public_config") {
  include_dirs = [ "shared" ]

  cflags = []
}

ohos_static_library("libshared") {
  sources = [
    "shared/config-parser.c",
    "shared/file-util.c",
    "shared/matrix.c",
    "shared/option-parser.c",
    "shared/os-compatibility.c",
  ]

  configs = [ ":libshared_config" ]

  public_configs = [ ":libshared_public_config" ]

  deps = []

  public_deps = [ "//third_party/wayland_standard:libwayland_server" ]
}

## Build libshared.a }}}

## Build libweston.so {{{
config("libweston_config") {
  visibility = [ ":*" ]

  include_dirs = [
    "//third_party/weston/include",
    "libweston",
    "//third_party/wayland_standard/src",
    "//third_party/wayland-ivi-extension_standard/include",
    "//third_party/weston",
  ]

  cflags = [
    "-Wall",
    "-Werror",
    "-Wno-unused-parameter",
    "-Wno-unused-function",
    "-Wno-missing-field-initializers",
    "-Wno-unused-variable",
    "-Wno-implicit-function-declaration",
  ]
}

config("libweston_public_config") {
  include_dirs = [
    "include",
    "libweston/backend-drm",
    "//third_party/bounds_checking_function/include",
  ]

  cflags = []
}

ohos_shared_library("libweston") {
  sources = [
    "libweston/animation.c",
    "libweston/bindings.c",
    "libweston/clipboard.c",
    "libweston/compositor.c",
    "libweston/content-protection.c",
    "libweston/data-device.c",
    "libweston/input.c",
    "libweston/launcher-direct.c",
    "libweston/launcher-util.c",
    "libweston/launcher-weston-launch.c",
    "libweston/linux-dmabuf.c",
    "libweston/linux-explicit-synchronization.c",
    "libweston/linux-sync-file.c",
    "libweston/pixel-formats.c",
    "libweston/pixman-renderer.c",
    "libweston/plugin-registry.c",
    "libweston/screenshooter.c",
    "libweston/tde-render-part.c",
    "libweston/touch-calibration.c",
    "libweston/vertex-clipping.c",
    "libweston/weston-direct-display.c",
    "libweston/zoom.c",
  ]

  configs = [
    ":libweston_config",
    ":display_gfx_public_config",
  ]

  public_configs = [
    ":libweston_public_config",
    ":drm-backend_public_config",
  ]

  deps = [
    ":libdisplay_gfx",
    "//foundation/graphic/standard/prebuilts/librarys/display_gralloc:libdisplay_gralloc",
    "//third_party/libdrm:libdrm",
    "//third_party/libinput:libinput-third",
    "//utils/native/base:utils",
  ]

  public_deps = [
    ":libshared",
    ":text_cursor_position_protocol",
    ":weston_content_protection_protocol",
    ":weston_direct_display_protocol",
    ":weston_touch_calibration_protocol",
    "//base/hiviewdfx/hilog/interfaces/native/innerkits:libhilog",
    "//third_party/libxkbcommon:libxkbcommon",
    "//third_party/pixman:libpixman",
    "//third_party/wayland-protocols_standard:input_timestamps_unstable_v1_protocol",
    "//third_party/wayland-protocols_standard:linux_dmabuf_unstable_v1_protocol",
    "//third_party/wayland-protocols_standard:linux_explicit_synchronization_unstable_v1_protocol",
    "//third_party/wayland-protocols_standard:pointer_constraints_unstable_v1_protocol",
    "//third_party/wayland-protocols_standard:presentation_time_protocol",
    "//third_party/wayland-protocols_standard:relative_pointer_unstable_v1_protocol",
    "//third_party/wayland-protocols_standard:viewporter_protocol",
    "//third_party/wayland-protocols_standard:xdg_output_unstable_v1_protocol",
  ]

  part_name = "graphic_standard"
  subsystem_name = "graphic"
}

## Build libweston.so }}}

## Build libexec_weston.a {{{
config("libexec_weston_config") {
  visibility = [ ":*" ]

  include_dirs = [
    "libweston",
    ".",
  ]

  cflags = [
    "-Wall",
    "-Werror",
    "-Wno-unused-parameter",
    "-Wno-missing-field-initializers",
    "-Wno-unused-variable",
  ]
}

config("libexec_weston_public_config") {
  include_dirs = []

  cflags = []
}

ohos_static_library("libexec_weston") {
  sources = [
    "compositor/main.c",
    "compositor/text-backend.c",
    "compositor/weston-screenshooter.c",
  ]

  configs = [ ":libexec_weston_config" ]

  public_configs = [ ":libexec_weston_public_config" ]

  deps = [
    ":weston_screenshooter_protocol",
    "//third_party/libinput:libinput-third",
    "//third_party/wayland-protocols_standard:input_method_unstable_v1_protocol",
    "//third_party/wayland-protocols_standard:text_input_unstable_v1_protocol",
  ]

  public_deps = [
    ":libweston",
    "//third_party/wayland_standard:wayland_core_protocol",
  ]
}

## Build libexec_weston.a }}}

## Build weston {{{
config("weston_config") {
  visibility = [ ":*" ]

  include_dirs = []

  cflags = [
    "-Wall",
    "-Werror",
  ]
}

config("weston_public_config") {
  include_dirs = []

  cflags = []
}

ohos_executable("weston") {
  install_enable = true

  sources = [ "compositor/executable.c" ]

  configs = [ ":weston_config" ]

  public_configs = [ ":weston_public_config" ]

  deps = [ ":libexec_weston" ]

  public_deps = []

  subsystem_name = "graphic"
  part_name = "graphic_standard"
}

## Build weston }}}

## Build libweston-desktop.so {{{
config("libweston-desktop_config") {
  visibility = [ ":*" ]

  include_dirs = [ "." ]

  cflags = [
    "-Wall",
    "-Werror",
    "-Wno-unused-function",
  ]
}

config("libweston-desktop_public_config") {
  include_dirs = [ "include/libweston-desktop" ]

  cflags = []
}

ohos_shared_library("libweston-desktop") {
  sources = [
    "libweston-desktop/client.c",
    "libweston-desktop/libweston-desktop.c",
    "libweston-desktop/seat.c",
    "libweston-desktop/surface.c",
    "libweston-desktop/wl-shell.c",
    "libweston-desktop/xdg-shell-v6.c",
    "libweston-desktop/xdg-shell.c",
  ]

  configs = [ ":libweston-desktop_config" ]

  public_configs = [ ":libweston-desktop_public_config" ]

  deps = []

  public_deps = [
    ":libexec_weston",
    "//third_party/wayland-protocols_standard:xdg_shell_protocol",
    "//third_party/wayland-protocols_standard:xdg_shell_unstable_v6_protocol",
  ]

  part_name = "graphic_standard"
  subsystem_name = "graphic"
}

## Build libweston-desktop.so }}}

## Build ivi-shell.so {{{
config("ivi-shell_config") {
  visibility = [ ":*" ]

  include_dirs = [ "." ]

  cflags = [
    "-Wall",
    "-Werror",
    "-Wno-unused-function",
  ]
}

config("ivi-shell_public_config") {
  include_dirs = []

  cflags = []
}

ohos_shared_library("ivi-shell") {
  sources = [
    "ivi-shell/ivi-layout-transition.c",
    "ivi-shell/ivi-layout.c",
    "ivi-shell/ivi-shell.c",
  ]

  configs = [ ":ivi-shell_config" ]

  public_configs = [ ":ivi-shell_public_config" ]

  deps = [
    ":ivi_application_protocol",
    ":libweston-desktop",
    "//third_party/wayland_standard:wayland_core_protocol",
  ]

  public_deps = []

  subsystem_name = "graphic"
  part_name = "graphic_standard"
}

## Build ivi-shell.so }}}

## Build drm-backend.so {{{
config("drm-backend_config") {
  visibility = [ ":*" ]

  include_dirs = [
    "include",
    "libweston",
    "libweston/backend-drm/auth",
    ".",
  ]

  cflags = [
    "-Wall",
    "-Wno-error",
    "-Wno-unused-parameter",
    "-Wno-missing-field-initializers",
    "-Wno-implicit-function-declaration",
    "-Wno-unused-variable",
    "-Wno-unused-function",
    "-Wno-sometimes-uninitialized",
  ]
}

config("drm-backend_public_config") {
  include_dirs = []

  cflags = []
}

ohos_executable("simple-preasenation") {
  sources = [
    "clients/presentation-shm.c",
    # "libweston/backend-drm/drm-gbm.c",
  ]

  configs = [ ":drm-backend_config" ]

  public_configs = [ ":drm-backend_public_config" ]

  deps = [
    "//third_party/wayland-protocols_standard:presentation_time_protocol",
    "//third_party/wayland_standard:libwayland_client",
    "//third_party/wayland_standard:libwayland_cursor",
    "//third_party/wayland_standard:wayland_core_protocol",
    "//third_party/weston:ivi_application_protocol",
  ]

  part_name = "graphic_standard"
  subsystem_name = "graphic"
}

ohos_shared_library("drm-backend") {
  sources = [
    "libweston/backend-drm/auth/wayland_drm_auth_server.c",
    "libweston/backend-drm/drm.c",
    "libweston/backend-drm/fb.c",
    "libweston/backend-drm/kms.c",
    "libweston/backend-drm/libbacklight.c",
    "libweston/backend-drm/modes.c",
    "libweston/backend-drm/state-helpers.c",
    "libweston/backend-drm/state-propose.c",
    "libweston/libinput-device.c",
    "libweston/libinput-seat.c",

    # "libweston/backend-drm/drm-gbm.c",
  ]

  configs = [ ":drm-backend_config" ]

  public_configs = [ ":drm-backend_public_config" ]

  deps = [
    ":drm_auth_protocol",
    ":libweston",
    "//third_party/libdrm:libdrm",
    "//third_party/libinput:libinput-third",
    "//third_party/wayland-protocols_standard:linux_dmabuf_unstable_v1_protocol",
    "//third_party/wayland-protocols_standard:presentation_time_protocol",
    "//third_party/wayland-protocols_standard:relative_pointer_unstable_v1_protocol",
    "//third_party/wayland_standard:wayland_core_protocol",
  ]

  public_deps = []

  part_name = "graphic_standard"
  subsystem_name = "graphic"
}

## Build drm-backend.so }}}

## Install weston.ini to /system/etc/weston.ini {{{
ohos_prebuilt_etc("weston.ini") {
  source = "weston.ini"
  part_name = "graphic_standard"
  subsystem_name = "graphic"
}

## Install weston.ini to /system/etc/weston.ini }}}

## Install weston.rc to /system/etc/init/weston.rc {{{
ohos_prebuilt_etc("weston.rc") {
  source = "weston.rc"
  relative_install_dir = "init"
  part_name = "graphic_standard"
  subsystem_name = "graphic"
}

## Install weston.rc to /system/etc/init/weston.rc }}}

# gl-renderer.so waiting for libEGL & libGLESv2
### Build gl-renderer.so {{{
#config("gl-renderer_config") {
#  visibility = [ ":*" ]
#
#  include_dirs = [
#    "//third_party/weston",
#    "//third_party/wayland_standard/egl",
#    "//third_party/weston/libweston",
#    "//third_party/wayland-ivi-extension_standard/include",
#  ]
#
#  cflags = [
#    "-Wno-return-type",
#    "-Wno-visibility",
#    "-Wno-unused-function",
#  ]
#}
#
#config("gl-renderer_public_config") {
#  include_dirs = [
#  ]
#
#  cflags = [
#  ]
#}
#
#ohos_shared_library("gl-renderer") {
#  sources = [
#    "libweston/renderer-gl/egl-glue.c",
#    "libweston/renderer-gl/gl-renderer.c",
#    "libweston/vertex-clipping.c",
#  ]
#
#  configs = [
#    ":gl-renderer_config",
#  ]
#
#  public_configs = [
#    ":gl-renderer_public_config",
#  ]
#
#  deps = [
#    ":libweston",
#    "//third_party/wayland-protocols_standard:linux_dmabuf_unstable_v1_protocol",
#  ]
#
#  asdk_deps = [
#    "shared_library:libEGL",
#    "shared_library:libGLESv2",
#  ]
#
#  public_deps = [
#  ]
#
#  subsystem_name = "graphic"
#  part_name = "graphic_standard"
#}
### Build gl-renderer.so }}}

## Prebuilt libdisplay_gfx.so {{{
config("display_gfx_public_config") {
  include_dirs = [ "//drivers/peripheral/display/interfaces/include" ]

  libs = [ "//device/hisilicon/hardware/display/libs/hispark_taurus/ext/libdisplay_gfx.z.so" ]
}

ohos_prebuilt_shared_library("libdisplay_gfx") {
  source = "//device/hisilicon/hardware/display/libs/hispark_taurus/ext/libdisplay_gfx.z.so"

  public_configs = [ ":display_gfx_public_config" ]

  part_name = "graphic_standard"
  subsystem_name = "graphic"
}
## Prebuilt libdisplay_gfx.so }}}
